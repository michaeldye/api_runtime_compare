buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.github.rholder:gradle-one-jar:1.0.4'
  }
}


subprojects {
  apply plugin: 'java'
  apply plugin: 'gradle-one-jar'

  repositories {
    mavenCentral()
  }

  project.version = '0.1.0-SNAPSHOT'
  project.group = 'com.lulzotron.perf'

  targetCompatibility = 1.8
  sourceCompatibility = 1.8

  apply from: "${rootProject.projectDir}/eclipse.gradle"

  tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
  }

  dependencies {
    compile 'ch.qos.logback:logback-classic:1.0.13'
  }
}

project(':common') {
}

project(':netty') {

  dependencies {
    compile project(':common')
    compile 'io.netty:netty-all:4.0.21.Final'
    runtime 'org.javassist:javassist:3.18.2-GA'
  }


  task oneJar(type: OneJar) {
    mainClass = 'com.lulzotron.perf.netty.Serve'
  }
}

project(':scala_spraycan') {
  repositories {
    mavenCentral()
    maven {
      url 'http://repo.spray.io/'
    }
  }

  apply plugin: 'scala'

  dependencies {
    compile project(':common')
    compile 'org.scala-lang:scala-library:2.10.4'
    compile 'com.typesafe.akka:akka-actor_2.10:2.3.0' // tied to spray version

    def spray_ver = '1.3.1'
    compile "io.spray:spray-can:$spray_ver"
    compile "io.spray:spray-routing:$spray_ver"
  }

  task oneJar(type: OneJar) {
    mainClass = 'com.lulzotron.perf.spraycan.Serve'
  }
}
